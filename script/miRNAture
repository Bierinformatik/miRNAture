#!/usr/bin/env perl

use strict;
use warnings;

use POSIX qw(strftime);
use Getopt::Long qw(HelpMessage GetOptionsFromArray);
use Pod::Usage;
use YAML::Tiny;
use Term::ANSIColor;
use Cwd qw(getcwd);

use lib "lib";
use Mir::ConfigurationFile;
use Mir::miRNAture;
use Mir::miRNAnchor;
use Mir::Eval;
use Mir::Summarise;
use MiRNAnchor::Tools;

### Input data
my $mode = ""; #run mode
my @strategy;
my $specie_genome_path = ""; #target specie genomes
my $data_path = ""; # Pre-calculated data from miRNAture
my $work_folder = ""; #output folder
my $name_specie = ""; #Scientific name of specie
my $specie_tag = ""; #Label of specie
my $subset_models; #Subset of models provided by the user
my $rep_cutoff = ""; # Homology cutoff to speed-up the searches 
my $maxthresholdBit = ""; # nBitscore threshold to clean data: 0-1
my $parallel_run = 0;
my $parallel_linux = 0;
my $current_path = getcwd;
my $stagesRun = "";
my $mirfix_path = "";
my $path_blast_queries = "";
my $user_folder = "";
my $debug = 0;
my $help;
my $man;
my %opts;
GetOptions (
	\%opts,
	'specie_genome|speG=s' => \$specie_genome_path,
	'specie_name|speN=s' => \$name_specie,
	'specie_tag|speT=s' => \$specie_tag,
	'subset_models|sublist=s' => \$subset_models,
	'workdir|w=s' => \$work_folder,
	'datadir|dataF=s' => \$data_path,
	'mirfix_path|mfx=s' => \$mirfix_path,
	'mode|m=s' => \$mode,
	'strategy|str=s' => \@strategy,
	'blastQueriesFolder|blstq=s' => \$path_blast_queries,
	'stage|stg=s' => \$stagesRun,
	'repetition_cutoff|rep=s' => \$rep_cutoff,
    'nbitscore_cutoff|nb_cut=f' => \$maxthresholdBit,
	'user_models|usrM=s' => \$user_folder,
	'help|h' => \$help,
	 man => \$man,
	'parallel_slurm|ps=i' => \$parallel_run,
	'parallel_linux|pe=i' => \$parallel_linux,
	'debug_mode|dbug=i' => \$debug,
) or pod2usage (
	-verbose => 99,
	-sections => "NAME"
);
pod2usage ( -verbose => 1 ) if $help;
pod2usage ( -verbose => 2 ) if $man;
pod2usage ( -message => "Seems that you did pass an empty flag, please refer to documentation.", -verbose => 1) if (keys %opts == 0 && length $specie_genome_path == 0);

@strategy = split (/,/, join(',',@strategy));

my $config_file = Mir::ConfigurationFile->new (
	stage => $stagesRun,
	specie_name => $name_specie,
	specie_tag => $specie_tag,
	specie_genome => $specie_genome_path,
	output_folder => $work_folder,
	data_path => $data_path,
	current_folder => $current_path,
	model_list => $subset_models,
	mirfix_path => $mirfix_path,
	mode => $mode,
	repetition_rules => $rep_cutoff,
    nbitscore_cut => $maxthresholdBit,
	blast_strategy => \@strategy,
	blast_queries_path => $path_blast_queries,
	user_folder => $user_folder,
	parallel_linux => $parallel_linux,
	parallel => $parallel_run,
	debug_mode => $debug
);
$config_file->start;
$config_file->generate_copy_genome;

# Build defaul models lists based on selected modes:  <22-04-22, cavelandiah> #
my $default_models = $config_file->models_list_hash();
$config_file->concatenate_models($default_models);


my $variables;
if ($config_file->stage =~ /^summarise$/){
	#I don't need to create a new summarise file,
	#but read the last one created.
	$variables = $config_file->read_last_file;
} else {
	$config_file->create_config_file;
	$config_file->write_header;
	$config_file->write_config_file($data_path);
	$variables = $config_file->read_final_file;
}

my $homology_searches = Mir::miRNAture->new(
	all_parameters => $variables
);

# Running modes: Homology and Validation
if ($config_file->stage =~ /^homology$/){
	$homology_searches->create_folder_environment(
		$config_file->config_file, $variables
	);
	$homology_searches->generate_file_mirnature(
		$variables, $config_file->config_file, $debug 
	);
	$homology_searches->run_miRNAture($variables);
	$homology_searches->clean_cache($variables, "Homology");
} elsif ($config_file->stage =~ /^validation$/){
	my $validation_mirnas = Mir::miRNAnchor->new(
		all_parameters => $variables
	);
	$validation_mirnas->include_folder_environment($config_file->config_file, $variables);
	$validation_mirnas->generate_file_mirnanchor($variables, $config_file->config_file);
	$validation_mirnas->run_miRNAnchor($variables);
	$homology_searches->clean_cache($variables, "Validation");
} elsif ($config_file->stage =~ /^complete$/){
	$homology_searches->create_folder_environment($config_file->config_file, $variables);
	$homology_searches->generate_file_mirnature($variables, $config_file->config_file, $debug);
	$homology_searches->run_miRNAture($variables);
	my $validation_mirnas = Mir::miRNAnchor->new(
		all_parameters => $variables
	);
	$validation_mirnas->include_folder_environment($config_file->config_file, $variables);
	$validation_mirnas->generate_file_mirnanchor($variables, $config_file->config_file);
	$validation_mirnas->run_miRNAnchor($variables);
	my $evaluation_results = Mir::Eval->new(
		all_parameters => $variables
	);
	$evaluation_results->create_folder_environment($config_file->config_file, $variables);
	$evaluation_results->test_input_files($config_file->config_file, $variables);
	$evaluation_results->perform_evaluation($variables); #Add MFE & final fasta
	my $summary_results = Mir::Summarise->new(
		all_parameters => $variables
	);
	$summary_results->generate_summary_file($config_file->config_file, $variables);
	$homology_searches->clean_cache($variables, "All");
} elsif ($config_file->stage =~ /^no_homology$/){
	my $validation_mirnas = Mir::miRNAnchor->new(
		all_parameters => $variables
	);
	$validation_mirnas->include_folder_environment($config_file->config_file, $variables);
	$validation_mirnas->generate_file_mirnanchor($variables, $config_file->config_file);
	$validation_mirnas->run_miRNAnchor($variables);
	my $evaluation_results = Mir::Eval->new(
		all_parameters => $variables
	);
	$evaluation_results->create_folder_environment($config_file->config_file, $variables);
	$evaluation_results->test_input_files($config_file->config_file, $variables);
	$evaluation_results->perform_evaluation($variables); #Add MFE & final fasta
	my $summary_results = Mir::Summarise->new(
		all_parameters => $variables
	);
	$summary_results->generate_summary_file($config_file->config_file, $variables);
	$homology_searches->clean_cache($variables, "All");
} elsif ($config_file->stage =~ /^evaluation$/){
	my $evaluation_results = Mir::Eval->new(
		all_parameters => $variables
	);
	$evaluation_results->create_folder_environment($config_file->config_file, $variables);
	$evaluation_results->test_input_files($config_file->config_file, $variables);
	$evaluation_results->perform_evaluation($variables);
} elsif ($config_file->stage =~ /^summarise/){
	my $summary_results = Mir::Summarise->new(
		all_parameters => $variables
	);
	$summary_results->generate_summary_file($config_file->config_file, $variables);
} else {
	print_error("Until now the stage: <".$config_file->stage."> does not exists!, try one of the available ones: 'homology', 'validation', 'evaluation', 'summarise', 'no_homology' or even better 'complete'.");
}
end_close();

__END__


=head1 NAME
C<miRNAture> - Computational detection of microRNA candidates

=head1 SYNOPSIS

./miRNAture [-options]

=head1 Authors:

I<Cristian A. Velandia Huerto>
I<Joerg Fallmann>
I<Peter F. Stadler>

=head1 OPTIONS

=over 12

=item -h/-help           

Print this documentation.

=item -man 

Prints an extended help page and exits.

=item -blstq/-blastQueriesFolder <PATH>

Path of blast query sequences in FASTA format to be searched on the subject sequence.

=item -dataF/-datadir <PATH>

Path to pre-calculated data directory containing RFAM and miRBase covariance, hidden markov models, 
and necessary files to run MIRfix. 

=item -debug_mode/dbug <0|1>

Activate the DEBUG mode on miRNAture run modes.

=item -mfx/-mirfix_path <PATH>

Alternative path of the MIRfix.py program.

=item -m/-mode <Blast, HMM, Other_CM, Infernal, Final>

Homology search modes: blast, hmm, mirbase, infernal, user and/or final. It is possible to perform individual analysis, but it is always recommended to include the I<final> option to merge multiple results.

=item -pe/-parallel_slurm <0|1>

Activate SLURM resource manager to submit parallel jobs (1) or not (0). 

=item -rep/-repetition_cutoff <relax,Number_Loci,Candidates_to_evaluate>

Setup number of maximum loci number that will be evaluated by the mature's annotation stage. By default, 
miRNAture will detect miRNA families that report high number of loci (> 200 loci). Then, it will select
the top 100 candidates in terms of alignment scores, as candidates for the validation stage (default,200,100). 
The designed values could be modified by the following flag: 'relax,Number_Loci,Candidates_to_evaluate'.
This option allows to the user to select the threshold values to detect repetitive families. The first 
parameter is <relax>, which tells miRNAture to change the default configuration. The next one, <Number_Loci> 
is the threshold of loci number to classify a family as repetitive. The last one, <Candidates_to_evaluate>, 
is the number of candidates prone to be evaluated in the next evaluation section. The rest candidates are
included as homology 'potential' candidates.

=item -str/-strategy <1,2,3,4,5,6,7,8,9,10>

This flag is blast mode specific. It corresponds to blast strategies that would be used to search miRNAs. It must be indicated along with -m I<Blast> flag. 

=item -stg/-stage <'homology','validation','evaluation','summarise','complete'>

Selects the stage to be run on miRNAture. The options are: 'homology', 'validation', 'evaluation', 'summarise' or 'complete'.

=item -speG/-specie_genome <PATH>

Path of target sequences to be analyzed in FASTA format.

=item -speN/-specie_name <Genera_specie>

Specie or sequence source's scientific name. The format must be: I<Genera_specie>, separated by '_'.

=item -speT/-specie_tag <TAG_NAME>

Experiment tag. Will help to identify the generated files along miRNA output files.

=item -sublist/-subset_models <FILE_WITH_CM_NAMES>

Target list of CMs to be searched on subject genome/sequences. If not indicated, miRNAture will run all RFAM v14.4 metazoan miRNA models.

=item -usrM/-user_models <PATH>

Directory with additional hidden Markov (HMMs) or covariance models (CMs) provided by the user. This must
be contain a corresponding HMMs/ and CMs/ folders, which the user models will be identified.

=item -w/-workdir <OUT_PATH>

Working directory path to write all miRNAture results.


=back

=head1 DESCRIPTION

B<miRNAture> detects I<bona fide> miRNA candidates through sequence homology 
searches and validation steps using structural alignments with pre-defined or/and 
modified miRNA-specific covariance models. The miRNAture pipeline is composed of
three modules: (1) Homology search operating on miRNA precursors, (2) prediction 
of the positioning of mature miRNAs within the precursor mature annotation, and 
(3) an Evaluation scheme designed to identify false positive miRNA annotations.
This multi-stage approach generates annotation files in BED/GFF3 from precursors 
and detected mature regions and corresponding FASTA files. At the same time, a 
summary file with the MFE, precursor length and number of loci of each annotated 
miRNA family.

=head1 AUTHOR

I<Cristian A. Velandia Huerto>
I<Joerg Fallmann>
I<Peter F. Stadler>

=head1 BUGS, CAVEATS, COMPLAINS or DONATIONS 
Write directly to cristian at bioinf.uni-leipzig.de

=cut 
